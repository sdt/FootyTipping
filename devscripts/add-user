#!/usr/bin/env perl
use Modern::Perl;

use Getopt::Lazy
    'username=s'     => 'Login name                 (required)',
    'password=s'     => 'Password                   (required)',
    'realname=s'     => 'Real name                  (required)',
    'email=s'        => 'Email address              (required)',
    '@competition=s' => 'Competition                (zero or more)',
    'team=s'         => 'Supported team             (optional)',
    -summary => 'Add a user',
    -usage => '%c %o',
    ;

use Tipping::Schema ();

GetOptions(-autohelp => sub {
        not (defined $username and defined $password and defined $realname)
    });

my $schema = Tipping::Schema->instance;
$schema->txn_do(sub {

    my $user = $schema->resultset('User')->create({
            user_name       => $username,
            password        => $password,
            real_name       => $realname,
            email           => $email,
        });

    for my $compname (@competition) {
        my $competition = $schema->resultset('Competition')->find({
                name => $compname
            });
        $user->add_to_competitions($competition);
    }

    if (defined $team) {
        my $supported_team = $schema->resultset('Team')->find({
                name => $team
            });
        $supported_team->add_to_supporters({ supporter => $user });
    }
});
